plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-android-extensions"
    id "kotlin-kapt"
    id "com.github.triplet.play" version "2.0.0-rc2"
    id 'io.sentry.android.gradle'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "me.iberger.enq"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 2
        versionName "0.1.0"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
            storeFile file(keystoreProperties["storeFile"])
            storePassword keystoreProperties["storePassword"]
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        abortOnError false
    }
}

play {
    serviceAccountCredentials = file("play_credentials.json")
    track = "beta"
    defaultToAppBundles = true
    resolutionStrategy = "auto"
}

sentry {
    autoProguardConfig true
    autoUpload true
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$v_kotlin"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$v_coroutines"
    implementation "androidx.core:core-ktx:$v_ktx"
    implementation "androidx.fragment:fragment-ktx:$v_ktx_fragments"
    implementation "androidx.appcompat:appcompat:$v_appcompat"
    implementation "androidx.constraintlayout:constraintlayout:$v_contraint_layout"
    implementation "com.google.android.material:material:$v_material"

    implementation "com.github.IIIuminator:JMusicBotAndroid:$v_jmusicbot"

    implementation "io.sentry:sentry-android:$v_sentry"
    implementation "org.slf4j:slf4j-nop:$v_slf4j"
    implementation "com.jakewharton.timber:timber:$v_timber"

    implementation "com.squareup.picasso:picasso:$v_picasso"
    implementation "com.squareup.moshi:moshi:$v_moshi"

    implementation "com.mikepenz:fastadapter:$v_fastadapter"
    implementation "com.mikepenz:fastadapter-commons:$v_fastadapter_commons"
    implementation "com.mikepenz:fastadapter-extensions:$v_fastadapter_extensions"

    implementation "com.mikepenz:iconics-core:$v_iconics"
    implementation "com.mikepenz:community-material-typeface:$v_iconics_tf_community_material@aar"
    implementation "ru.ztrap.iconics:core-kt:$v_iconics_ktx"
}
